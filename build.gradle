plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

group 'io.github.huynhsamha'
version '0.0.1-SNAPSHOT-LOCAL'

repositories {
    mavenCentral()
}

java {
    withJavadocJar()
    withSourcesJar()
}

dependencies {
    implementation('io.prometheus:simpleclient:0.10.0')
    implementation('io.prometheus:simpleclient_hotspot:0.10.0')
    implementation('io.prometheus:simpleclient_httpserver:0.10.0')
    implementation('io.prometheus:simpleclient_servlet:0.10.0')
    implementation('io.prometheus:simpleclient_jetty:0.10.0')
    implementation('io.prometheus:simpleclient_pushgateway:0.10.0')

    implementation('org.rocksdb:rocksdbjni:6.15.2')

    testImplementation('org.apache.commons:commons-lang3:3.11')

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
}

test {
    useJUnitPlatform()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'jrocksdb-exporter'
            from components.java
            pom {
                name = 'Java RocksDB Prometheus Exporter'
                description = 'Prometheus Exporter for RocksJava. Monitor Java RocksDB by using Prometheus and Grafana.'
                url = 'https://github.com/huynhsamha/java-rocksdb-prometheus-exporter/'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'huynhsamha'
                        name = 'Ha Huynh'
                        email = 'huynhsamha@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/huynhsamha/java-rocksdb-prometheus-exporter.git'
                    developerConnection = 'scm:git:ssh://github.com/huynhsamha/java-rocksdb-prometheus-exporter.git'
                    url = 'https://github.com/huynhsamha/java-rocksdb-prometheus-exporter/'
                }
                issueManagement {
                    system = 'GitHub'
                    url = 'https://github.com/huynhsamha/java-rocksdb-prometheus-exporter/issues'
                }
                organization {
                    name = 'io.github.huynhsamha'
                    url = 'https://github.com/huynhsamha'
                }
            }
        }
    }

    repositories {
        maven {
            def localReleasesRepoUrl = "$buildDir/repos/releases"
            def localSnapshotsRepoUrl = "$buildDir/repos/snapshots"
            def snapshotsRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"

            url = version.endsWith('LOCAL') ?
                    (version.endsWith('SNAPSHOT-LOCAL') ? localSnapshotsRepoUrl : localReleasesRepoUrl)
                    :
                    (version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl)

            credentials {
                username "$sonatypeUsername"
                password "$sonatypePassword"
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}
